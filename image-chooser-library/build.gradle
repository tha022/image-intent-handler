apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.seniordev" // "com.kbeanie"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required {
        has("release") && gradle.taskGraph.hasTask("uploadArchives")
    }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        pom.groupId = "com.seniordev"
        pom.artifactId = "image-chooser-library"
        pom.version = "1.5"

        repository(url: 'http://ci.bipper.com/artifactory/ext-release-local') { // sonatypeRepo ext-release-local
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Image Chooser Library'
            packaging 'aar'
            description 'Image Chooser Library for Android Applications'
            //url 'https://github.com/coomar2841/image-chooser-library'
            url 'https://github.com/tha022/image-intent-handler'

            scm {
                url                 'scm:git:ssh://git@ci.bipper.com:3687/image-chooser-library'
                connection          'scm:git:ssh://git@ci.bipper.com:3687/image-chooser-library'
                developerConnection 'scm:git:ssh://git@ci.bipper.com:3687/image-chooser-library'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'tha022'
                    name 'Thomas Vervik'
                    email 'thomas.vervik@seniordevonly.com'
                }
            }
        }
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            keyAlias digReleaseKeyAlias
            keyPassword digReleaseKeyPassword
            storeFile file(digReleaseStoreFile)
            storePassword digReleaseStorePassword
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:23.0.1'
}
